plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group 'team.floracore'
version rootProject.version

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    implementation project(':common')
    compileOnly project(':common:loader-utils')
    compileOnly 'org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly "net.kyori:adventure-platform-bukkit:4.3.0"
}


shadowJar {
    archiveFileName = 'floracore-plugin.jarinjar'

    dependencies {
        include(dependency('team.floracore:.*'))
    }

    relocate 'org.mariadb.jdbc', 'team.floracore.lib.mariadb'
    relocate 'com.mysql', 'team.floracore.lib.mysql'
    relocate 'org.postgresql', 'team.floracore.lib.postgresql'
    relocate 'com.zaxxer.hikari', 'team.floracore.lib.hikari'
    relocate 'io.lettuce.core', 'team.floracore.lib.lettuce'
}

artifacts {
    archives shadowJar
}

processResources {
    def props = [version: project.parent.version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}